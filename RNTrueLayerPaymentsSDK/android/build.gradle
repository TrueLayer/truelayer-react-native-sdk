import groovy.json.JsonSlurper

def getVersionFromPackageJson() {
    def packageJsonFile = file('../package.json')
    def packageJson = new JsonSlurper().parseText(packageJsonFile.text)
    return packageJson.version
}

buildscript {
  ext.safeExtGet = {prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }
  repositories {
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath("com.android.tools.build:gradle:7.4.2")
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.20"
  }
}

def isNewArchitectureEnabled() {
   return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

apply plugin: 'com.android.library'
apply plugin: 'com.facebook.react'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 33)
  defaultConfig {
    def packageJsonVersion = getVersionFromPackageJson()
    buildConfigField "String", "RN_TL_SDK_VERSION", "\"${packageJsonVersion}\""
    buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
  }
  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += ['src/newarch/java']
      } else {
        java.srcDirs += ['src/oldarch/java']
      }
    }
  }
}

repositories {
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url "$projectDir/../node_modules/react-native/android"
  }
  mavenCentral()
  google()
}

dependencies {
  implementation 'com.facebook.react:react-native:+'
  implementation 'com.truelayer.payments:ui:2.4.0'
}